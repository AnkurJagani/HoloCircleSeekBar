// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.0'
    }
}

allprojects {
    repositories {
        mavenCentral()
        jcenter()
    }
}

ext {
    versionMajor = 2
    versionMinor = 2
    versionPatch = 1

    versionCode = versionMajor * 1000 + versionMinor * 100 + versionPatch
    versionName = versionMajor + "." + versionMinor + "." + versionPatch

    minSdkVersion = 15
    targetSdkVersion = 22
    compileSdkVersion = 22
    buildToolsVersion = '22.0.1'
}

ext.libraries = [
        otto           : 'com.squareup:otto:1.3.+',
        butterknife    : 'com.jakewharton:butterknife:6.1.+',
        appcompat      : 'com.android.support:appcompat-v7:22.0.+',
        supportv4      : 'com.android.support:support-v4:22.0.+',
        supportv13     : 'com.android.support:support-v13:22.0.+',
        jobqeue        : 'com.path:android-priority-jobqueue:1.1.+',
        double_espresso: 'com.jakewharton.espresso:espresso:1.1-r3',
        picasso        : 'com.squareup.picasso:picasso:2.5.+',
        nineoldandroid :'com.nineoldandroids:library:2.4.0',
        ads  : 'com.google.android.gms:play-services-ads:7.0.0',
        cardview       : 'com.android.support:cardview-v7:22.0.+',
        material_dialogs: 'com.afollestad:material-dialogs:0.+',
        palette                : 'com.android.support:palette-v7:22.0.+'
        ,
        recyclerview   : 'com.android.support:recyclerview-v7:22.0.+',
        recyclerview_multiselector: 'com.bignerdranch.android:recyclerview-multiselect:+'

]

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}


project.ext.preDexLibs = project.hasProperty('disablePreDex')

subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        } else if ("com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}
